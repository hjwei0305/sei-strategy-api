apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

// 扩展定义
ext {
    // 启动类
    MainClass = 'com.domlin.strategy.RestApplication'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    // 本项目依赖模块
    compile project(':sei-strategy-api-api')
    // SEI平台依赖包
    compile("com.changhong.sei:sei-cloud-starter:$sei_version")
    // 第三方组件依赖包
    compile("mysql:mysql-connector-java:$mysqlVersion")
    compile("org.apache.curator:curator-framework:4.2.0")
    compile("org.apache.curator:curator-recipes:4.2.0")
    // 测试依赖包
    // compile("com.changhong.sei:sei-test-starter:$sei_version")
    testCompile("com.changhong.sei:sei-test-starter:$sei_version")
    implementation 'com.changhong.sei:sei-serial-sdk:6.+' // 给号服务
    implementation "com.changhong.sei:sei-basic-v6-api:6.0.1.199"
}

// 处理资源配置文件
processResources {
    // 替换bootstrap.yaml文件中@project_version@占位符的值
    filesMatching('bootstrap.yaml') {
        filter org.apache.tools.ant.filters.ReplaceTokens, tokens: [
                project_name       : project.property('name'),
                project_version    : project.property('version'),
                project_description: project.property('description')
        ]
    }
    // 默认application文件中的配置从配置中心获取
    excludes = ["**/application.yaml", "**/application.properties"]
}

//打ar包
bootJar {
    enabled = true
    archiveFileName = "${rootProject.name}.${archiveExtension.get()}"
    mainClassName = MainClass
    manifest {
        attributes("Manifest-Version": "6.0")
        attributes("Created-By": "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString())
        attributes("Implementation-Title": "$project.rootProject.name")
        attributes("Implementation-Version": "$project.version")
        attributes("Implementation-Vendor": "rcsit.cn")
        attributes("Description": "$project.description")
        attributes("Build-Time": new Date().format("yyyy-MM-dd HH:mm:ss"))
    }
}

jar {
    enabled = true
    exclude('bootstrap.*')
    exclude('application.*')
    exclude('com/domlin/strategy/RestApplication.*')
    manifest {
        attributes("Manifest-Version": "6.0")
        attributes("Created-By": "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString())
        attributes("Implementation-Title": "$project.rootProject.name")
        attributes("Implementation-Version": "$project.version")
        attributes("Implementation-Vendor": "rcsit.cn")
        attributes("Description": "$project.description")
        attributes("Build-Time": new Date().format("yyyy-MM-dd HH:mm:ss"))
    }
}

/////////////////////////////////////上传Maven仓库////////////////////////////////////////
// 打包源代码
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

// 如果希望gradle install，安装到.m2本地仓库，参考下面的内容
install {
    repositories.mavenInstaller {
        pom.version = "$project.version"
        pom.artifactId = "$project.name"
        pom.groupId = "$project.group"
    }
}

//上传到nexus
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: MAVEN_REPO_URL) {
                authentication(userName: NEXUS_USERNAME, password: NEXUS_PASSWORD)
            }
            pom.version = "$project.version"
            pom.artifactId = "$project.name"
            pom.groupId = "$project.group"
        }
    }
    return 'Success'
}

